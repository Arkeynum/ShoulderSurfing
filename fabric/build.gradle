plugins {
	id 'java'
	id 'eclipse'
	id 'fabric-loom'
	id 'com.modrinth.minotaur'
	id 'me.hypherionmc.cursegradle'
}

tasks.withType(Jar) {
	archivesBaseName("${mod_name}-Fabric")
}

eclipse {
	project {
		name = "${mod_name}Fabric"
	}
}

sourceSets {
	api
}

dependencies {
	implementation(project(":common")) {
		transitive = false
	}
	
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.officialMojangMappings()
	
	modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
	modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${forgeconfigapiport_version}"
	modImplementation "mcp.mobius.waila:wthit:fabric-${wthit_version}"
	modImplementation "lol.bai:badpackets:fabric-${badpackets_version}"
	modImplementation "curse.maven:jade-324717:${jade_version_fabric}"
}

loom {
	mixin.defaultRefmapName = "${mod_id}.refmap.json"
	
	runs {
		client {
			client()
			setConfigName("${mod_name} Fabric Client")
			ideConfigGenerated(true)
			runDir("../run")
		}
		server {
			server()
			setConfigName("${mod_name} Fabric Server")
			ideConfigGenerated(true)
			runDir("../run")
		}
	}
}

processResources {
	outputs.upToDateWhen {
		false
	}
	
	from project(":common").sourceSets.main.resources
	
	inputs.property "version", mod_version
	inputs.property "mod_name", mod_name
	
	filesMatching('fabric.mod.json') {
		expand('version': mod_version)
	}
	
	filesMatching('pack.mcmeta') {
		expand('mod_name': mod_name)
	}
	
	duplicatesStrategy = 'exclude'
}

compileJava {
	source project(":common").sourceSets.api.allSource
	source project(":common").sourceSets.main.allSource
}

task apiJar(type: Jar) {
	from project(":common").sourceSets.api.output
	from project(":common").sourceSets.api.allSource
	
	from (sourceSets.api.resources) {
		include 'fabric.mod.json'
	}
	
	inputs.property "version", mod_version
	
	filesMatching('fabric.mod.json') {
		expand('version': mod_version)
	}
	
	archiveClassifier = "API"
}

curseforge {
	apiKey = project.hasProperty("curse_api_key") ? curse_api_key : ''
	project {
		id = curse_project_id
		changelog = file('../changelog.txt').canRead() ? file('../changelog.txt').text : ''
		changelogType = 'text'
		releaseType = 'release'
		addGameVersion 'Fabric'
		fabric_compatible_minecraft_versions.split(",").each {
			addGameVersion(it)
		}
		mainArtifact(remapJar) {
			displayName = "${mod_name}-Fabric-${minecraft_version}-${mod_version}"
		}
		addArtifact apiJar
		relations {
			requiredDependency 'fabric-api'
			requiredDependency 'forge-config-api-port-fabric'
		}
	}
	options {
		javaVersionAutoDetect = false
		forgeGradleIntegration = false
	}
}

modrinth {
	token = project.hasProperty("modrinth_api_key") ? modrinth_api_key : ''
	projectId = modrinth_project_id
	versionName = "${mod_name}-Fabric-${minecraft_version}-${mod_version}"
	versionNumber = "${minecraft_version}-${mod_version}"
	changelog = file('../changelog.txt').canRead() ? file('../changelog.txt').text : ''
	versionType = 'release'
	uploadFile = remapJar
	fabric_compatible_minecraft_versions.split(",").each {
		gameVersions.add(it)
	}
	loaders = ['fabric']
	additionalFiles = [apiJar]
	dependencies {
		required.project 'fabric-api'
		required.project 'forge-config-api-port'
	}
}

tasks.build.finalizedBy("apiJar")