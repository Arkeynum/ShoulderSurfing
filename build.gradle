plugins {
	id 'java'
	id 'com.modrinth.minotaur' version '2.+' apply false
	id 'me.hypherionmc.cursegradle' version '2.+' apply false
	id 'fabric-loom' version '1.3.+' apply false
}

subprojects {
	java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)
	javadoc.options.addStringOption('Xdoclint:none', '-quiet')
	
	repositories {
		mavenCentral()
		
		maven {
			name = 'Sponge'
			url = 'https://repo.spongepowered.org/repository/maven-public/'
		}
		maven {
			name = "Fuzs Mod Resources"
			url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
		}
		maven {
			name = 'WTHIT'
			url = 'https://maven2.bai.lol'
		}
		maven {
			url = "https://www.cursemaven.com"
			content {
				includeGroup "curse.maven"
			}
		}
	}
	
	tasks.withType(Jar).configureEach {
		version("${minecraft_version}-${mod_version}")
		manifest {
			attributes([
				'Specification-Title': mod_name,
				'Specification-Vendor': mod_author,
				'Specification-Version': mod_version,
				'Implementation-Title': project.name,
				'Implementation-Version': mod_version,
				'Implementation-Vendor': mod_author,
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
				'Built-On-Java': "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
				'Build-On-Minecraft': minecraft_version
			])
		}
	}
	
	tasks.withType(JavaCompile).configureEach {
		options.encoding = 'UTF-8'
		options.getRelease().set(JavaLanguageVersion.of(java_version).asInt())
	}
}
