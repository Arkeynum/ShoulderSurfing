plugins {
	id 'org.quiltmc.loom' version '0.12.+'
	id 'eclipse'
}

repositories {
	maven {
		name = 'Fuzs Mod Resources'
		url = 'https://raw.githubusercontent.com/Fuzss/modresources/main/maven/'
	}
}

archivesBaseName = "${mod_name}-Common-${minecraft_version}"

eclipse {
	project {
		name = mod_name + 'Common'
	}
}

sourceSets {
	api {
		compileClasspath += sourceSets.main.compileClasspath
	}
	main {
		java {
			compileClasspath += sourceSets.api.output
			runtimeClasspath += sourceSets.api.output
		}
	}
	testplugin {
		java {
			compileClasspath += sourceSets.main.compileClasspath
		}
	}
}

configurations {
	apiImplementation.extendsFrom(implementation)
	apiRuntimeOnly.extendsFrom(runtimeOnly)
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings fileTree(dir: "../mapping", include: "**.jar")
	
	compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
	modImplementation "net.minecraftforge:forgeconfigapiport-fabric:1.0.0"
}

loom {
	remapArchives = false
	
	mixin {
		useLegacyMixinAp = false
	}
	
	runs {
		client {
			client()
			setConfigName("Common Client")
			ideConfigGenerated(false)
			runDir("../run")
		}
		server {
			server()
			setConfigName("Common Server")
			ideConfigGenerated(false)
			runDir("../run")
		}
	}
}
