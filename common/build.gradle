plugins {
	id 'java'
	id 'eclipse'
	id 'fabric-loom'
}

tasks.withType(Jar) {
	archivesBaseName("${mod_name}-Common")
}

eclipse {
	project {
		name = "${mod_name}Common"
	}
}

sourceSets {
	api {
		compileClasspath += sourceSets.main.compileClasspath
	}
	main {
		java {
			compileClasspath += sourceSets.api.output
			runtimeClasspath += sourceSets.api.output
		}
	}
	testplugin {
		java {
			compileClasspath += sourceSets.main.compileClasspath
		}
	}
}

configurations {
	apiImplementation.extendsFrom(implementation)
	apiRuntimeOnly.extendsFrom(runtimeOnly)
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings fileTree(dir: "../mapping", include: "**.jar")
	
	compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
	modImplementation "net.minecraftforge:forgeconfigapiport-fabric:${forgeconfigapiport_version}"
	implementation 'com.electronwill.night-config:core:3.6.3'
	implementation 'com.electronwill.night-config:toml:3.6.3'
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

loom {
	mixin {
		defaultRefmapName = "${mod_id}.refmap.json"
		useLegacyMixinAp = false
	}
	
	runs {
		client {
			client()
			setConfigName("${mod_name} Common Client")
			ideConfigGenerated(false)
			runDir("../run")
		}
		server {
			server()
			setConfigName("${mod_name} Common Server")
			ideConfigGenerated(false)
			runDir("../run")
		}
	}
}
